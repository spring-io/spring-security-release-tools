plugins {
	id "io.spring.convention.spring-gradle-plugin"
	id "com.gradle.plugin-publish" version "1.2.1"
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

gradlePlugin {
	website = "https://github.com/spring-io/spring-security-release-tools/tree/main/release-plugin"
	vcsUrl = "https://github.com/spring-io/spring-security-release-tools.git"
	plugins {
		springSecurityReleasePlugin {
			id = "io.spring.security.release"
			displayName = "Spring Security Release Plugin"
			description = "Spring Security Release Plugin is a Gradle plugin that provides common tasks for automating Spring releases."
			tags.addAll("spring", "github", "release")
			implementationClass = "io.spring.gradle.plugin.release.SpringReleasePlugin"
		}
	}
}

artifactory {
	publish {
		defaults {
			publications("pluginMaven", "springSecurityReleasePluginPluginMarkerMaven")
		}
	}
}

dependencies {
	management platform(project(":dependencies"))
	implementation project(":github-api")
	implementation project(":sagan-api")
	implementation project(":spring-security-release-tools-core")

	testImplementation "org.junit.jupiter:junit-jupiter"
	testRuntimeOnly "org.junit.platform:junit-platform-launcher"

	testImplementation "org.assertj:assertj-core"
	testImplementation "org.mockito:mockito-core"
}

shadowJar {
	archiveClassifier = ""
	relocate "com.fasterxml", "shaded.com.fasterxml"
}
